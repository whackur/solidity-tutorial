// SPDX-License-Identifier: MIT
pragma solidity ^0.8.30;

import {EIP712} from "@openzeppelin/contracts/utils/cryptography/EIP712.sol";
import {ECDSA} from "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract Voucher is EIP712 {
    bytes32 private immutable _VOUCHER_TYPEHASH =
        keccak256("Voucher(address token,address signer,address redeemer,uint256 voucherId,uint256 amount)");

    mapping(uint256 => bool) public usedVouchers;

    event VoucherRedeemed(
        address indexed token, address indexed signer, address indexed redeemer, uint256 voucherId, uint256 amount
    );

    constructor() EIP712("MyEIP712App", "1") {}

    /**
     * @param token The ERC20 token contract address.
     * @param signer The address that signed the voucher.
     * @param redeemer The address that will redeem the voucher.
     * @param voucherId A unique ID for the voucher, serving as a nonce.
     * @param amount The amount associated with the voucher.
     * @param signature The 65-byte signature generated by the signer.
     */
    function redeemVoucher(
        address token,
        address signer,
        address redeemer,
        uint256 voucherId,
        uint256 amount,
        bytes calldata signature
    ) external {
        require(!usedVouchers[voucherId], "Voucher already redeemed");
        require(msg.sender == redeemer, "Only the specified redeemer can call this");

        bytes32 structHash = keccak256(abi.encode(_VOUCHER_TYPEHASH, token, signer, redeemer, voucherId, amount));

        bytes32 digest = _hashTypedDataV4(structHash);
        address recoveredSigner = ECDSA.recover(digest, signature);

        require(recoveredSigner == signer, "Invalid signature");
        require(recoveredSigner != address(0), "Invalid signature (recovered zero address)");

        usedVouchers[voucherId] = true;

        IERC20(token).transferFrom(signer, redeemer, amount);

        emit VoucherRedeemed(token, signer, redeemer, voucherId, amount);
    }
}
